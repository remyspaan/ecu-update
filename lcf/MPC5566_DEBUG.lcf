/* lcf file for MPC5566 (debug RAM version)                          */

/*              3 MB Flash, 128KB SRAM                                     */

MEMORY
{
    pseudo_rom:         org = 0x40000000,   len = 0x00011000
    init:               org = 0x40011000,   len = 0x00001000
    exception_handlers: org = 0x40012000,   len = 0x00001000
    internal_ram:       org = 0x40013000,   len = 0x0000A000
    heap  :             org = 0x4001D000,   len = 0x00001000 /* Heap starts */
    stack :             org = 0x4001E000,   len = 0x00001000 /* Start location for Stack */
}

SECTIONS
{
    GROUP : {
      .init : {}
      .init_vle (VLECODE) : {
        *(.init)
        *(.init_vle)
      }
    } > init

    .__exception_handlers : {} > exception_handlers

    GROUP : {
        .text (TEXT) ALIGN(0x1000) : {}
        .text_vle (VLECODE) ALIGN(0x1000): {
             *(.text)
             *(.text_vle)
         }
        .rodata (CONST) : {
            *(.rdata)
            *(.rodata)
        }
        .ctors : {}
        .dtors : {}
        extab : {}
        extabindex : {}
    } > pseudo_rom

    GROUP : {    
       .__uninitialized_intc_handlertable ALIGN(0x10) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
    } > internal_ram
}

/* Freescale CodeWarrior compiler address designations */

_stack_addr = ADDR(stack)+SIZEOF(stack);
_stack_end  = ADDR(stack);
_heap_addr  = ADDR(heap);
_heap_end   = ADDR(heap)+SIZEOF(heap);

/* Exceptions Handlers Location (used in Exceptions.c for IVPR initialization) */

EXCEPTION_HANDLERS = ADDR(exception_handlers);
